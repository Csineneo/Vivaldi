import("//build/config/chrome_build.gni")

if (is_win) {
  if (is_official_build) {
    update_template_instance("//components/viz/test:test_support") {
      cflags = [ "/wd4723" ]  # potential divide by 0.
    }
  }

  foreach(_target,
          [
            "//chrome/test:browser_tests",
            "//components:components_unittests",
            "//content/test:content_browsertests",
            "//content/test:content_unittests",
          ]) {
    update_template_instance(_target) {
      configs -= [ "//build/config/win:default_incremental_linking" ]
      configs += [ "//build/config/win:no_incremental_linking" ]
    }
  }
}

if (is_mac) {
  update_template_instance("//chrome:chrome_framework") {
    ldflags += [
      "-Wl,-rpath,@loader_path/../../../../../Frameworks",
    ]
  }

  foreach(_target,
          [
            "//chrome/test:browser_tests",
            "//chrome/test:interactive_ui_tests",
            "//chrome/test:sync_integration_tests",
            "//chrome/test:unit_tests",
            "//components:components_unittests",
          ]) {
    update_template_instance(_target) {
      if (!defined(ldflags)) {
        ldflags = []
      }
      ldflags += [
        "-Wl,-rpath,@executable_path/../../third_party/sparkle_lib/",
      ]
    }
  }
}

if (is_linux) {
  update_target("//chrome:chrome_initial") {
    configs -= [ "//vivaldi/gn/config:disable_new_dtags" ]

    # Find lib/libffmpeg.so without the executable wrapper script. Note that
    # RUNPATH is used over RPATH since the wrapper script uses LD_LIBRARY_PATH
    # which has lower priority than RPATH but higher than RUNPATH.
    ldflags += [
      "-rpath",
      "\$ORIGIN/lib/",
      "-Wl,--enable-new-dtags",
    ]
  }
  if (target_cpu == "arm64") {
    update_template_instance("//third_party/swiftshader/third_party/llvm-7.0:swiftshader_llvm") {
      if (!defined(cflags)) {
        cflags = []
      }
      cflags += [ "-Wno-defaulted-function-deleted" ]
    }
    update_template_instance("//third_party/swiftshader/src/Reactor:swiftshader_reactor") {
      if (!defined(cflags)) {
        cflags = []
      }
      cflags += [ "-Wno-defaulted-function-deleted" ]
    }
  }
}
