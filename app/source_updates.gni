import("//vivaldi/gn/config/features.gni")
import("//vivaldi/gn/config/product.gni")
import("//vivaldi/gn/config/version.gni")
import("//vivaldi/app/resources/untranslated.gni")

update_target("//base:base") {
  if (!defined(sources)) {
    sources = []
  }
  sources += [
    "//vivaldi/app/vivaldi_apptools.h",
  ]
}

update_target("//base:base_static") {
  sources += [
    "//vivaldi/app/vivaldi_apptools.cpp",
    "//vivaldi/app/vivaldi_apptools.h",
    "//vivaldi/app/vivaldi_constants.cc",
    "//vivaldi/app/vivaldi_constants.h",
  ]
  if (is_win) {
    sources += [
      "//vivaldi/update_notifier/update_notifier_switches.cc",
      "//vivaldi/update_notifier/update_notifier_switches.h",
    ]
  }
}

update_target("//chrome:visual_elements_resources") {
  sources = []
  sources += [
    "//vivaldi/app/other_resources/$VIVALDI_RELEASE_KIND/win8/Logo.png",
    "//vivaldi/app/other_resources/$VIVALDI_RELEASE_KIND/win8/SmallLogo.png",
    "//vivaldi/app/other_resources/vivaldi.VisualElementsManifest.xml",
  ]
}

update_target("//chrome/common:non_code_constants") {
  sources += [
    "//vivaldi/app/vivaldi_version_constants.cc",
    "//vivaldi/app/vivaldi_version_constants.h",
  ]
}

update_target("//components/version_info:version_info") {
  sources += [
    "//vivaldi/app/vivaldi_version_info.cc",
    "//vivaldi/app/vivaldi_version_info.h",
  ]
}

if (is_mac) {
  update_target("//chrome:chrome_resources") {
    sources -= [
      "app/theme/$branding_path_component/mac/app.icns",
      "app/theme/$branding_path_component/mac/document.icns",
    ]
    sources += [
      "//vivaldi/app/resources/theme/$VIVALDI_RELEASE_KIND/mac/app.icns",
      "//vivaldi/app/resources/theme/$VIVALDI_RELEASE_KIND/mac/document.icns",
    ]
  }

  update_target("//chrome:chrome_framework_resources") {
    sources -= [
      "app/theme/default_100_percent/$branding_path_component/product_logo_32.png",
    ]
    sources += [
      "//vivaldi/app/resources/theme/default_100_percent/vivaldi/product_logo_32.png",
    ]
  }
}

foreach(
    _target_config,
    [
      [
        "//chrome/app:generated_resources",
        "//vivaldi/app/resources/generated/vivaldi_generated_resources.grd",
        {
          pak_file_prefix="generated_resources_"
        },
      ],
      [
        "//chrome/app:chromium_strings",
        "//vivaldi/app/resources/strings/vivaldi_strings.grd",
        {
          have_strings_flag = false
          merge_extra_grit_defines = ["-D", "vivaldi_chromium_file"]
        },
      ],
      [
        "//chrome/app:google_chrome_strings",
        "//vivaldi/app/resources/strings/vivaldi_strings.grd",
        {
          translation_dir_setting = "resources"
          pak_file_prefix="google_chrome_strings_"
        },
      ],
      [
        "//ui/strings:ui_strings",
        "//vivaldi/app/resources/ui_strings/vivaldi_ui_strings.grd",
        {
          translation_dir_setting = "strings"
          pak_file_prefix="ui_strings_"
        },
      ],
      [
        "//chrome/app/resources:locale_settings",
        "//vivaldi/app/resources/locale/vivaldi_locale_strings.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//chrome/browser:resources",
        "//vivaldi/app/resources/browser/vivaldi_browser_resources.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//chrome/renderer:resources",
        "//vivaldi/app/resources/renderer/vivaldi_renderer_resources.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//chrome/app/resources:platform_locale_settings",
        "//vivaldi/app/resources/platform_locale_settings/vivaldi_platform_locale_settings.grd",
        {
          have_strings_flag = false
          pak_file_prefix="platform_locale_settings_"
        },
      ],
      [
        "//chrome/app/theme:theme_resources",
        "//vivaldi/app/resources/theme/vivaldi_theme.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//chrome/app/theme:chrome_unscaled_resources",
        "//vivaldi/app/resources/theme/vivaldi_unscaled_resources.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//components/resources:components_resources",
        "//vivaldi/app/resources/components_resources/vivaldi_components.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//ui/resources:ui_resources_grd",
        "//vivaldi/app/resources/ui/vivaldi_ui_resources.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//components/strings:components_strings",
        "//vivaldi/app/resources/components_strings/vivaldi_components_strings.grd",
        {
          translation_dir_setting = "strings"
          pak_file_prefix="components_strings_"
        },
      ],
      [
        "//components/strings:components_chromium_strings",
        "//vivaldi/app/resources/components/vivaldi_components.grd",
        {
          have_strings_flag = false
          merge_extra_grit_defines = ["-D", "vivaldi_chromium_file"]
        },
      ],
      [
        "//components/strings:components_google_chrome_strings",
        "//vivaldi/app/resources/components/vivaldi_components.grd",
        {
          translation_dir_setting = "strings"
          pak_file_prefix="components_google_chrome_strings_"
        },
      ],
      [
        "//components/strings:components_locale_settings",
        "//vivaldi/app/resources/components_locale_settings/vivaldi_components_locale_settings.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//components/resources:components_scaled_resources",
        "//vivaldi/app/resources/components_scaled/vivaldi_components_scaled.grd",
        {
          have_strings_flag = false
        },
      ],
      [
        "//content/app/strings:strings",
        "//vivaldi/app/resources/content/vivaldi_content_strings.grd",
        {
          translation_dir_setting = "strings"
          pak_file_prefix="content_strings_"
        },
      ],
    ]) {
  _target = _target_config[0]
  vivaldi_source = _target_config[1]
  _flags = {
  }
  _flags = _target_config[2]
  resource_dir = get_label_info(_target, "dir")
  have_strings_flag =
      !defined(_flags.have_strings_flag) || _flags.have_strings_flag
  have_translation = defined(_flags.translation_dir_setting)
  if (have_translation) {
    translation_dir_setting = _flags.translation_dir_setting
  }
  extra_outputs = []
  if (defined(_flags.pak_file_prefix)) {
    foreach (locale, vivaldi_pending_locales) {
      extra_outputs += [ _flags.pak_file_prefix+"$locale.pak" ]
    }
  }
  merge_extra_grit_defines = []
  if (defined(_flags.merge_extra_grit_defines)) {
    merge_extra_grit_defines = _flags.merge_extra_grit_defines
  }

  update_template_instance(_target) {
    #print (target_name)
    actual_target_name = target_name
    temp = rebase_path(get_label_info(_target, "target_gen_dir"), root_gen_dir)
    target_dir = "$root_gen_dir/vivaldi_resources/$temp/$actual_target_name"
    target_filename = "$actual_target_name.grd"
    full_target_path = "$target_dir/$target_filename"
    main_resource_dir = resource_dir
    merge_main_file = source
    vivaldi_resource_path = vivaldi_source
    combined_target_dir = target_dir
    have_strings = have_strings_flag
    if (have_translation) {
      translation_dir = translation_dir_setting
    }
    if (!defined(grit_flags)) {
      grit_flags = []
    }
    grit_flags += extra_grit_defines + merge_extra_grit_defines
    outputs += extra_outputs
  }
  if (generate_untranslated && have_strings_flag) {
    untranslated_target = get_label_info(_target, "dir") + ":Extract_strings_merge_"+ get_label_info(_target, "name")
    update_target("//vivaldi/app:generate_untranslated") {
      deps += [untranslated_target]
    }
  }
}
