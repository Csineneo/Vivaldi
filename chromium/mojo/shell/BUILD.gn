# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

group("all") {
  testonly = true
  deps = [
    ":shell",
    "//mojo/shell/background",
    "//mojo/shell/runner",
    "//mojo/shell/standalone",
    "//mojo/shell/tests",
  ]
}

source_set("shell") {
  output_name = "mojo_shell"
  sources = [
    "connect_params.cc",
    "connect_params.h",
    "connect_util.cc",
    "connect_util.h",
    "loader.h",
    "native_runner.h",
    "native_runner_delegate.h",
    "shell.cc",
    "shell.h",
    "switches.cc",
    "switches.h",
  ]

  deps = [
    "//base",
    "//base/third_party/dynamic_annotations",
    "//mojo/common",
    "//mojo/public/cpp/bindings",
    "//mojo/services/catalog/public/interfaces",
    "//mojo/shell/public/cpp:sources",
    "//mojo/shell/public/interfaces",
    "//mojo/util:filename_util",
    "//url",
  ]

  public_deps = [
    # ApplicationManager exposes and uses PackageManager types in its header.
    "//mojo/services/catalog:lib",
  ]

  data_deps = [
    ":manifest",
  ]

  # For mojo/shell/application_loader.h
  allow_circular_includes_from = [ "//mojo/services/catalog:lib" ]
}

mojo_application_manifest("manifest") {
  application_name = "shell"
  source = "manifest.json"
}
