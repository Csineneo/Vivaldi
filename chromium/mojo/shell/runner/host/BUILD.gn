# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

group("host") {
  testonly = true

  deps = [
    ":lib",
    ":mojo_runner_host_unittests",
  ]
}

config("native_application_support_with_gles2") {
  defines = [ "NATIVE_APPLICATION_USE_GLES2_IMPL" ]
}

source_set("native_application_support") {
  sources = [
    "native_application_support.cc",
    "native_application_support.h",
  ]

  deps = [
    "//base",
    "//mojo/platform_handle:platform_handle_impl",
    "//mojo/shell",
  ]

  if (!is_mac) {
    configs += [ ":native_application_support_with_gles2" ]
    deps += [ "//mojo/gles2" ]
  }

  # This target has to include the public thunk headers, which generally
  # shouldn't be included without picking an implementation. We are providing
  # the implementation but the thunk header target cannot declare that we are
  # permitted to include it since it's in the public SDK and we are not.
  # Suppress include checking so we can still check the rest of the targets in
  # this file.
  check_includes = false
}

source_set("child_process_base") {
  sources = [
    "child_process_base.cc",
    "child_process_base.h",
  ]

  deps = [
    "//base",
    "//mojo/edk/system",
    "//mojo/message_pump",
    "//mojo/platform_handle:platform_handle_impl",
    "//mojo/shell",
    "//mojo/shell/runner:init",
    "//mojo/shell/runner/child:interfaces",
    "//mojo/shell/runner/common",
  ]
}

source_set("lib") {
  sources = [
    "child_process.cc",
    "child_process.h",
    "child_process_host.cc",
    "child_process_host.h",
    "in_process_native_runner.cc",
    "in_process_native_runner.h",
    "out_of_process_native_runner.cc",
    "out_of_process_native_runner.h",
  ]

  deps = [
    ":child_process_base",
    ":native_application_support",
    "//base",
    "//base:base_static",
    "//base:i18n",
    "//mojo/edk/system",
    "//mojo/message_pump",
    "//mojo/platform_handle:platform_handle_impl",
    "//mojo/shell",
    "//mojo/shell/runner:init",
    "//mojo/shell/runner/child:interfaces",
    "//mojo/shell/runner/common",
  ]

  if (is_linux && !is_android) {
    sources += [
      "linux_sandbox.cc",
      "linux_sandbox.h",
    ]

    deps += [
      "//sandbox/linux:sandbox",
      "//sandbox/linux:sandbox_services",
      "//sandbox/linux:seccomp_bpf",
      "//sandbox/linux:seccomp_bpf_helpers",
    ]
  }
}

test("mojo_runner_host_unittests") {
  sources = [
    "child_process_host_unittest.cc",
    "host_unittests.cc",
    "in_process_native_runner_unittest.cc",
  ]

  deps = [
    ":lib",
    "//base",
    "//base/test:test_support",
    "//mojo/edk/system",
    "//mojo/message_pump",
    "//mojo/shell",
    "//mojo/shell/runner:init",
    "//mojo/shell/runner/common",
    "//testing/gtest",
  ]
}
