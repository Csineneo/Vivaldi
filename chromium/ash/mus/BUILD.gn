# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/repack.gni")

source_set("lib") {
  sources = [
    "context_menu_mus.cc",
    "context_menu_mus.h",
    "keyboard_ui_mus.cc",
    "keyboard_ui_mus.h",
    "shelf_delegate_mus.cc",
    "shelf_delegate_mus.h",
    "shell_delegate_mus.cc",
    "shell_delegate_mus.h",
    "stub_context_factory.cc",
    "stub_context_factory.h",
    "sysui_application.cc",
    "sysui_application.h",
  ]

  deps = [
    "//ash",
    "//cc",
    "//cc/surfaces",
    "//components/mus/common:mus_common",
    "//components/mus/public/cpp",
    "//components/mus/public/interfaces",
    "//components/user_manager",
    "//device/bluetooth",
    "//mash/wm/public/interfaces",
    "//mojo/common:common_base",
    "//mojo/converters/geometry",
    "//mojo/converters/input_events",
    "//mojo/services/tracing/public/cpp",
    "//mojo/shell/public/cpp",
    "//skia",
    "//ui/aura",
    "//ui/events",
    "//ui/events/devices",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/keyboard:mojom",
    "//ui/message_center",
    "//ui/mojo/init",
    "//ui/platform_window/stub",
    "//ui/resources",
    "//ui/strings",
    "//ui/views",
    "//ui/views/mus:for_mojo_application",
  ]

  data_deps = [
    "//ash/strings:ash_test_strings",
    "//ash/resources:ash_test_resources_100_percent",
    "//ash/resources:ash_test_resources_200_percent",
  ]

  if (is_chromeos) {
    deps += [ "//chromeos" ]
  }
}

mojo_application_manifest("manifest") {
  application_name = "ash_sysui"
  source = "manifest.json"
}

mojo_native_application("mus") {
  output_name = "ash_sysui"
  sources = [
    "main.cc",
  ]
  deps = [
    ":lib",
    "//mojo/shell/public/cpp",
  ]

  data_deps = [
    ":manifest",
    "//components/mus",
  ]
}
