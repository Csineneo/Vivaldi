# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

mojom("interfaces_use_new_wrappers") {
  sources = [
    "accessibility_manager.mojom",
    "animations.mojom",
    "cursor.mojom",
    "display_manager.mojom",
    "event_matcher.mojom",
    "gpu_memory_buffer.mojom",
    "gpu_service.mojom",
    "ime.mojom",
    "mus_constants.mojom",
    "user_access_manager.mojom",
    "user_activity_monitor.mojom",
    "window_manager_constants.mojom",
    "window_server_test.mojom",
  ]

  import_dirs = [
    get_path_info("../../../..", "abspath"),
    "//mojo/services",
  ]

  public_deps = [
    "//cc/ipc:interfaces",
    "//gpu/ipc/common:interfaces",
    "//services/ui/public/interfaces/display",
    "//ui/events/mojo:interfaces",
    "//ui/gfx/geometry/mojo",
    "//ui/gfx/mojo",
    "//ui/platform_window/mojo:interfaces",
  ]
}

# TODO(yzshen): merge this target into the one above.
mojom("interfaces") {
  sources = [
    "clipboard.mojom",
    "window_manager.mojom",
    "window_manager_window_tree_factory.mojom",
    "window_tree.mojom",
    "window_tree_constants.mojom",
    "window_tree_host.mojom",
  ]

  import_dirs = [
    get_path_info("../../../..", "abspath"),
    "//mojo/services",
  ]

  public_deps = [
    ":constants",
    ":interfaces_use_new_wrappers",
    "//cc/ipc:interfaces",
    "//gpu/ipc/common:interfaces",
    "//services/ui/public/interfaces/display",
    "//ui/events/mojo:interfaces",
    "//ui/gfx/geometry/mojo",
    "//ui/gfx/mojo",
    "//ui/platform_window/mojo:interfaces",
  ]

  use_new_wrapper_types = false
}

mojom("constants") {
  sources = [
    "constants.mojom",
  ]
}

test("ui_struct_traits_unittests") {
  testonly = true

  sources = [
    "display/display_struct_traits_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//mojo/edk/test:run_all_unittests",
    "//services/ui/public/interfaces/display:test_interfaces",
    "//testing/gtest",
    "//ui/display/types",
    "//ui/gfx:test_support",
  ]
}
