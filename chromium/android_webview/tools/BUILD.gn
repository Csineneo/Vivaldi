# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")
import("//testing/test.gni")

# Mark all targets as test only.
testonly = true

group("tools") {
  deps = [
    ":system_webview_shell_apk",
    ":system_webview_shell_layout_test_apk",
    ":system_webview_shell_page_cycler_apk",
  ]
}

# GYP: //android_webview/android_webview_shell.gypi:system_webview_shell_apk
android_apk("system_webview_shell_apk") {
  apk_name = "SystemWebViewShell"
  DEPRECATED_java_in_dir = "WebViewShell/src"
  android_manifest = "WebViewShell/AndroidManifest.xml"
  deps = [
    ":system_webview_shell_apk_resources",
    "//base:base_java",
  ]
}

android_resources("system_webview_shell_apk_resources") {
  resource_dirs = [ "WebViewShell/res" ]
  custom_package = "org.chromium.webview_shell"
}

# GYP: //android_webview/android_webview_shell.gypi:system_webview_shell_page_cycler_apk
instrumentation_test_apk("system_webview_shell_page_cycler_apk") {
  apk_name = "SystemWebViewShellPageCycler"
  apk_under_test = ":system_webview_shell_apk"
  android_manifest = "PageCycler/AndroidManifest.xml"
  DEPRECATED_java_in_dir = "PageCycler/src"
  deps = [
    "//base:base_java",
    "//base:base_java_test_support",
    "//content/public/android:content_java",
    "//content/public/test/android:content_java_test_support",
    "//testing/android/reporter:reporter_java",
  ]
}

# GYP: //android_webview/android_webview_shell.gypi:system_webview_shell_layout_test_apk
instrumentation_test_apk("system_webview_shell_layout_test_apk") {
  apk_name = "SystemWebViewShellLayoutTest"
  apk_under_test = ":system_webview_shell_apk"
  android_manifest = "WebViewShellTest/AndroidManifest.xml"
  DEPRECATED_java_in_dir = "WebViewShellTest/src"
  isolate_file = "../system_webview_shell_test_apk.isolate"
  deps = [
    "//base:base_java",
    "//base:base_java_test_support",
    "//testing/android/reporter:reporter_java",
  ]
}
