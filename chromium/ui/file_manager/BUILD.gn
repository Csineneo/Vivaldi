# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/grit/grit_rule.gni")

grit("resources") {
  source = "file_manager_resources.grd"
  outputs = [
    "grit/file_manager_resources.h",
    "grit/file_manager_resources_map.cc",
    "grit/file_manager_resources_map.h",
    "file_manager_resources.pak",
    "file_manager_resources.rc",
  ]
  if (enable_nacl) {
    zip_archiver_pexe = "$root_gen_dir/ui/file_manager/zip_archiver_pnacl.pexe.js"
    grit_flags = [
      "-E",
      "zip_archiver_pexe=" + rebase_path(zip_archiver_pexe, root_build_dir),
      "-D",
      "enable_nacl",
    ]
    deps = [
      "//ui/file_manager/zip_archiver/:zip_archiver",
    ]
  }
}

component("file_manager") {
  sources = [
    "file_manager_export.h",
    "file_manager_resource_util.cc",
    "file_manager_resource_util.h",
  ]
  public_deps = [
    ":resources",
    "//base",
  ]
  defines = [ "FILE_MANAGER_IMPLEMENTATION" ]
}
