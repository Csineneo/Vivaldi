# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

group("client") {
  visibility = [ "//blimp" ]

  deps = []

  if (is_android) {
    deps += [
      "//blimp/client/app:blimp_apk",
      "//blimp/client/app:blimp_apk_incremental",
    ]
  }

  if (is_linux && !is_chromeos && use_x11) {
    deps += [ "//blimp/client/app:blimp_shell" ]
  }
}

group("test_binaries") {
  visibility = [ "//blimp:blimp_tests" ]

  testonly = true

  if (is_android) {
    deps = [
      "//blimp/client/app:blimp_test_apk",
    ]
  }
}

group("unit_tests") {
  visibility = [ "//blimp:*" ]

  testonly = true

  deps = [
    "//blimp/client/app:app_unit_tests",
    "//blimp/client/core:unit_tests",
  ]
}

if (is_android) {
  # This is the list of targets that the tests need to depend on in order to add
  # the java classes for their native counterparts to the test apk.
  # We could directly include them in the test target, but in order to
  # keep the visibility of the //blimp/client/core:core_java target restricted,
  # we make it visible to this group (which is testonly) and depend on this
  # group instead.
  # We could not add "//blimp:blimp_unittests" to the visibility list of
  # //blimp/client/core:core_java, since the targets that actually need to
  # depend on it are generated by the test template (see testing/test.gni). So
  # it was better to add this indirection and expose it to this testonly target,
  # which can be used in this file only.
  java_group("blimp_unittests_java_deps") {
    visibility = [ "//blimp:*" ]

    testonly = true

    deps = [
      "//base:base_java",
      "//blimp/client/core:core_java",
      "//net/android:net_java",
      "//ui/android:ui_java",
    ]
  }
}
