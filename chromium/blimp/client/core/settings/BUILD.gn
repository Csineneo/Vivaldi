# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("settings") {
  visibility = [ "//blimp/client/*" ]

  sources = [
    "blimp_settings_delegate.h",
    "settings_feature.cc",
    "settings_feature.h",
  ]

  deps = [
    "//blimp/common",
    "//net",
  ]

  public_deps = [
    "//base",
    "//blimp/net",
  ]

  if (is_android) {
    sources += [
      "android/blimp_settings_android.cc",
      "android/blimp_settings_android.h",
    ]

    deps += [
      ":settings_jni_headers",
      "//blimp/client/core/session",
      "//blimp/common",
    ]
  }
}

if (is_android) {
  android_library("settings_java") {
    visibility = [ "//blimp/client/*" ]

    java_files = [
      "android/java/src/org/chromium/blimp/core/settings/AboutBlimpPreferences.java",
      "android/java/src/org/chromium/blimp/core/settings/BlimpPreferencesDelegate.java",
    ]

    # TODO(xingliu): Clean resources files in the future. http://crbug.com/630687.
    deps = [
      "//base:base_java",
      "//blimp/client:blimp_java_resources",
      "//blimp/client/core/common:common_java",
      "//blimp/client/public:public_headers_java",
      "//components/signin/core/browser/android:java",
      "//components/sync/android:sync_java",
      "//third_party/android_tools:android_support_v7_appcompat_java",
    ]
  }

  generate_jni("settings_jni_headers") {
    visibility = [ ":*" ]

    sources = [
      "android/java/src/org/chromium/blimp/core/settings/AboutBlimpPreferences.java",
    ]

    jni_package = "blimp/client/core/settings"
  }
}
