# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
import("//testing/test.gni")
import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//tools/grit/repack.gni")

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  source_set("mus") {
    sources = [
      "android_loader.cc",
      "android_loader.h",
    ]

    deps = [
      ":lib",
      "public/interfaces",
      "//mojo/public/cpp/bindings",
      "//mojo/services/tracing/public/interfaces",
      "//mojo/shell",
      "//mojo/shell/public/cpp:sources",
    ]
  }
} else {
  mojo_native_application("mus") {
    sources = [
      "main.cc",
    ]

    deps = [
      ":lib",
      ":manifest",
      "//mojo/platform_handle:for_shared_library",
      "//mojo/shell/public/cpp:sources",
    ]

    if (is_win) {
      deps += [ ":copy_gl_libraries" ]
    }
  }

  mojo_application_manifest("manifest") {
    application_name = "mus"
    source = "manifest.json"
  }
}

if (is_win) {
  copy("copy_gl_libraries") {
    deps = [
      "//third_party/angle:libEGL",
      "//third_party/angle:libGLESv2",
    ]

    sources = [
      "$root_shlib_dir/libEGL.dll",
      "$root_shlib_dir/libGLESv2.dll",
    ]

    outputs = [
      "$root_out_dir/mus/{{source_file_part}}",
    ]
  }
}

source_set("lib") {
  sources = [
    "mus_app.cc",
    "mus_app.h",
  ]

  deps = [
    ":resources_100",
    ":resources_200",
    ":resources_strings",
    "//base",
    "//cc",
    "//cc/surfaces",
    "//components/mus/common",
    "//components/mus/gles2",
    "//components/mus/public/interfaces",
    "//components/mus/surfaces",
    "//components/mus/ws:lib",
    "//components/resource_provider/public/cpp",
    "//mojo/common:common_base",
    "//mojo/services/tracing/public/cpp",
    "//mojo/shell/public/cpp",
    "//ui/events",
    "//ui/gl:gl",
    "//ui/platform_window:platform_impls",
    "//ui/platform_window:platform_window",
  ]

  data_deps = [
    "//components/resource_provider",
  ]

  if (use_x11) {
    public_configs = [ "//build/config/linux:x11" ]
    public_deps = [
      "//ui/events/platform/x11",
    ]
  }

  if (use_ozone) {
    deps += [ "//ui/ozone:ozone" ]
  }
}

repack("resources_strings") {
  sources = [
    "$root_gen_dir/ui/strings/app_locale_settings_en-US.pak",
  ]
  output = "$root_out_dir/mus/resources/mus_app_resources_strings.pak"
  deps = [
    "//ui/strings",
  ]
}

repack("resources_100") {
  sources = [
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
  ]
  output = "$root_out_dir/mus/resources/mus_app_resources_100.pak"
  deps = [
    "//ui/resources",
  ]
}

repack("resources_200") {
  sources = [
    "$root_gen_dir/ui/resources/ui_resources_200_percent.pak",
  ]
  output = "$root_out_dir/mus/resources/mus_app_resources_200.pak"
  deps = [
    "//ui/resources",
  ]
}
