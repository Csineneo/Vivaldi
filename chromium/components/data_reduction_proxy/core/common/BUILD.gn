# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")

# Variables:
#   deps: Extra dependencies.
template("common_tmpl") {
  source_set(target_name) {
    sources = [
      "data_reduction_proxy_bypass_action_list.h",
      "data_reduction_proxy_bypass_type_list.h",
      "data_reduction_proxy_client_config_parser.cc",
      "data_reduction_proxy_client_config_parser.h",
      "data_reduction_proxy_config_values.h",
      "data_reduction_proxy_event_creator.cc",
      "data_reduction_proxy_event_creator.h",
      "data_reduction_proxy_event_storage_delegate.h",
      "data_reduction_proxy_event_store.cc",
      "data_reduction_proxy_event_store.h",
      "data_reduction_proxy_headers.cc",
      "data_reduction_proxy_headers.h",
      "data_reduction_proxy_params.cc",
      "data_reduction_proxy_params.h",
      "data_reduction_proxy_pref_names.cc",
      "data_reduction_proxy_pref_names.h",
      "data_reduction_proxy_switches.cc",
      "data_reduction_proxy_switches.h",
      "lofi_decider.h",
      "lofi_ui_service.h",
    ]

    public_deps = [
      ":version_header",
    ]
    deps = [
      "//base",
      "//components/data_reduction_proxy/proto:data_reduction_proxy_proto",
      "//components/variations",
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}

if (is_android) {
  common_tmpl("common_small") {
    deps = [
      "//net:net_small",
      "//url:url_lib_use_icu_alternatives_on_android",
    ]
  }
}

common_tmpl("common") {
  deps = [
    "//net",
    "//url",
  ]
}

source_set("test_support") {
  testonly = true
  sources = [
    "data_reduction_proxy_event_storage_delegate_test_utils.cc",
    "data_reduction_proxy_event_storage_delegate_test_utils.h",
    "data_reduction_proxy_headers_test_utils.cc",
    "data_reduction_proxy_headers_test_utils.h",
    "data_reduction_proxy_params_test_utils.cc",
    "data_reduction_proxy_params_test_utils.h",
  ]

  public_deps = [
    ":common",
  ]
  deps = [
    "//base",
    "//net",
    "//net:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "data_reduction_proxy_client_config_parser_unittest.cc",
    "data_reduction_proxy_event_store_unittest.cc",
    "data_reduction_proxy_headers_unittest.cc",
    "data_reduction_proxy_params_unittest.cc",
  ]

  deps = [
    ":common",
    ":test_support",
    "//base",
    "//base/test:test_support",

    # TODO this dependency seems wrong, but
    # data_reduction_proxy_event_store_unittest.cc includes a file from it.
    "//components/data_reduction_proxy/core/browser",
    "//components/data_reduction_proxy/proto:data_reduction_proxy_proto",
    "//components/variations",
    "//net:test_support",
    "//testing/gtest",
  ]
}

process_version("version_header") {
  template_file = "version.h.in"
  output = "$target_gen_dir/version.h"
}
