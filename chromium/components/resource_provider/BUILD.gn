# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/mojo_application.gni")
import("//mojo/public/mojo_application_manifest.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")

if (is_android) {
  import("//build/config/android/rules.gni")

  java_library_path = "$target_out_dir/java_library.dex.jar"

  mojo_android_application("resource_provider") {
    input_so =
        "$root_shlib_dir/${shlib_prefix}resource_provider_lib$shlib_extension"
    input_dex_jar = java_library_path
    deps = [
      ":java_library",
      ":resource_provider_lib",
      "//mojo/shell/public/cpp",
    ]
  }

  shared_library("resource_provider_lib") {
    sources = [
      "android/android_hooks.cc",
      "main.cc",
    ]

    deps = [
      ":jni_headers",
      ":lib",
      "//base",
      "//build/config/sanitizers:deps",
      "//components/resource_provider/public/interfaces",
      "//mojo/platform_handle:for_shared_library",
      "//mojo/public/c/system:for_shared_library",
      "//mojo/shell/public/cpp",
      "//url",
    ]
  }

  generate_jni("jni_headers") {
    sources = [
      "android/java/org/chromium/resource_provider/Main.java",
    ]
    jni_package = "components/resource_provider"
  }

  android_library("java_classes") {
    java_files = [ "android/java/org/chromium/resource_provider/Main.java" ]

    deps = [
      "//base:base_java",
    ]
  }

  android_standalone_library("java_library") {
    dex_path = java_library_path

    deps = [
      ":java_classes",
    ]
  }
} else {
  mojo_native_application("resource_provider") {
    sources = [
      "main.cc",
    ]

    deps = [
      ":lib",
      "//base",
      "//components/resource_provider/public/interfaces",
      "//mojo/shell/public/cpp",
      "//url",
    ]

    data_deps = [
      ":manifest",
    ]
  }

  mojo_application_manifest("manifest") {
    application_name = "resource_provider"
    source = "manifest.json"
  }
}

source_set("lib") {
  sources = [
    "file_utils.cc",
    "file_utils.h",
    "resource_provider_app.cc",
    "resource_provider_app.h",
    "resource_provider_impl.cc",
    "resource_provider_impl.h",
  ]

  deps = [
    "//base",
    "//components/resource_provider/public/interfaces",
    "//mojo/common:common_base",
    "//mojo/platform_handle",
    "//mojo/services/tracing/public/cpp",
    "//mojo/shell/public/cpp",
    "//url",
  ]
}

test("resource_provider_unittests") {
  sources = [
    "file_utils_unittest.cc",
    "resource_provider_unittest.cc",
  ]

  deps = [
    ":lib",
    ":test_interfaces",
    "//base",
    "//components/resource_provider/public/interfaces",
    "//mojo/shell/background:main",
    "//mojo/shell/public/cpp:shell_test_support",
    "//mojo/shell/public/cpp/test:run_all_shelltests",
    "//testing/gtest",
    "//url",
  ]

  data_deps = [
    ":test_app",
    ":unittests_manifest",
  ]
}

mojo_native_application("test_app") {
  output_name = "resource_provider_test_app"
  testonly = true

  sources = [
    "resource_provider_test_app.cc",
  ]

  resources = [
    "//components/test/data/resource_provider/sample",
    "//components/test/data/resource_provider/dir",
  ]

  deps = [
    ":test_interfaces",
    "//base",
    "//components/resource_provider/public/cpp",
    "//components/resource_provider/public/interfaces",
    "//mojo/shell/public/cpp",
    "//mojo/shell/public/interfaces",
  ]

  data_deps = [
    ":resource_provider",
    ":test_manifest",
  ]
}

mojo_application_manifest("test_manifest") {
  application_name = "resource_provider_test_app"
  source = "test_manifest.json"
}

mojo_application_manifest("unittests_manifest") {
  type = "exe"
  application_name = "resource_provider_unittests"
  source = "unittests_manifest.json"
}

mojom("test_interfaces") {
  sources = [
    "test.mojom",
  ]
}
