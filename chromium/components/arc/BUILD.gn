# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

static_library("arc") {
  sources = [
    "arc_bridge_bootstrap.cc",
    "arc_bridge_bootstrap.h",
    "arc_bridge_service.cc",
    "arc_bridge_service.h",
    "arc_bridge_service_impl.cc",
    "arc_bridge_service_impl.h",
    "arc_service_manager.cc",
    "arc_service_manager.h",
    "auth/arc_auth_service.h",
    "clipboard/arc_clipboard_bridge.cc",
    "clipboard/arc_clipboard_bridge.h",
    "ime/arc_ime_bridge.cc",
    "ime/arc_ime_bridge.h",
    "ime/arc_ime_ipc_host.cc",
    "ime/arc_ime_ipc_host.h",
    "input/arc_input_bridge.h",
    "input/arc_input_bridge_impl.cc",
    "input/arc_input_bridge_impl.h",
    "power/arc_power_bridge.cc",
    "power/arc_power_bridge.h",
    "settings/arc_settings_bridge.cc",
    "settings/arc_settings_bridge.h",
    "video/arc_video_bridge.cc",
    "video/arc_video_bridge.h",
    "video/video_host_delegate.h",
  ]

  deps = [
    "//base",
    "//base:prefs",
    "//chromeos",
    "//chromeos:power_manager_proto",
    "//components/signin/core/account_id",
    "//ipc:ipc",
    "//ipc/mojo:mojo",
    "//third_party/mojo/src/mojo/edk/system",
    "//ui/arc",
    "//ui/aura",
    "//ui/base:base",
    "//ui/base/ime",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
  ]

  public_deps = [
    ":arc_bindings",
  ]
}

mojom("arc_bindings") {
  sources = [
    "common/app.mojom",
    "common/arc_bridge.mojom",
    "common/auth.mojom",
    "common/clipboard.mojom",
    "common/ime.mojom",
    "common/input.mojom",
    "common/notifications.mojom",
    "common/power.mojom",
    "common/process.mojom",
    "common/settings.mojom",
    "common/video.mojom",
  ]
}

static_library("arc_test_support") {
  testonly = true
  sources = [
    "test/fake_app_instance.cc",
    "test/fake_app_instance.h",
    "test/fake_arc_bridge_instance.cc",
    "test/fake_arc_bridge_instance.h",
    "test/fake_arc_bridge_service.cc",
    "test/fake_arc_bridge_service.h",
  ]

  deps = [
    ":arc",
    ":arc_bindings",
    "//base",
    "//mojo/common:common_base",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "arc_bridge_service_unittest.cc",
  ]

  deps = [
    ":arc",
    ":arc_bindings",
    ":arc_test_support",
    "//base",
    "//chromeos",
    "//ipc/mojo:mojo",
    "//mojo/public/cpp/environment:environment",
    "//mojo/public/cpp/system:system",
    "//testing/gtest",
    "//ui/aura",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
  ]
}
