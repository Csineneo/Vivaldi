# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

source_set("test_support_win8") {
  sources = [
    "test/open_with_dialog_async.cc",
    "test/open_with_dialog_async.h",
    "test/open_with_dialog_controller.cc",
    "test/open_with_dialog_controller.h",
    "test/ui_automation_client.cc",
    "test/ui_automation_client.h",
  ]

  # TODO(jschuh): crbug.com/167187 fix size_t to int truncations.
  configs += [ "//build/config/compiler:no_size_t_to_int_warning" ]

  deps = [
    ":test_registrar_constants",
    "//base",
  ]
}

source_set("test_registrar_constants") {
  sources = [
    "test/test_registrar_constants.cc",
    "test/test_registrar_constants.h",
  ]
}

executable("test_registrar") {
  sources = [
    "test/test_registrar.cc",
    "test/test_registrar.rc",
    "test/test_registrar.rgs",
    "test/test_registrar_resource.h",
  ]

  configs -= [ "//build/config/win:console" ]
  configs += [ "//build/config/win:windowed" ]

  deps = [
    ":test_registrar_constants",
    "//base",
    "//build/config/sanitizers:deps",
    "//build/win:default_exe_manifest",

    # Chrome is the default viewer process currently used by the tests.
    # TODO(robertshield): Investigate building a standalone metro viewer
    # process.
    "//chrome",
  ]
}

copy("visual_elements_resources") {
  sources = [
    "resources/Logo.png",
    "resources/SecondaryTile.png",
    "resources/SmallLogo.png",
    "resources/chrome.VisualElementsManifest.xml",
  ]

  outputs = [
    "$root_out_dir/{{source_file_part}}",
  ]
}
